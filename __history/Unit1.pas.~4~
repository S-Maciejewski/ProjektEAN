unit Unit1;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.StdCtrls;

type
   TForm1 = class(TForm)
    Button1: TButton;
    Edit1: TEdit;
    Label1: TLabel;
    Label2: TLabel;
    Label3: TLabel;
    Label4: TLabel;
    Label5: TLabel;
    procedure Button1Click(Sender: TObject);


  private
    { Private declarations }
  public
    { Public declarations }
  end;

  vector = array [0..6] of Extended;
  vector1 = array [0..6] of Extended;
  vector2 = array [0..6] of Extended;
  matrix = array [0..3, 0..6] of Extended;

var
  Form1: TForm1;
  n: Integer;
  x: vector;
  f: vector;
  a: matrix;
  st: Integer;
  formNumber: Integer;

implementation

{$R *.dfm}

procedure naturalsplinecoeffns (n      : Integer;
                                x,f    : vector;
                                var a  : matrix;
                                var st : Integer);
{---------------------------------------------------------------------------}
{                                                                           }
{  The procedure naturalsplinecoeffns calculates the coefficients of the    }
{  natural cubic spline interpolant for a function given by its values at   }
{  nodes.                                                                   }
{  Data:                                                                    }
{    n  - number of interpolation nodes minus 1 (the nodes are numbered     }
{         from 0 to n),                                                     }
{    x  - an array containing the values of nodes,                          }
{    f  - an array containing the values of function.                       }
{  Result:                                                                  }
{    a  - an array of spline coefficients (the element a[k,i] contains the  }
{         coefficient before x^k, where k=0,1,2,3, for the interval         }
{         <x[i], x[i+1]>; i=0,1,...,n-1).                                   }
{  Other parameters:                                                        }
{    st - a variable which within the procedure naturalsplinevalue is       }
{         assigned the value of:                                            }
{           1, if n<1,                                                      }
{           2, if there exist x[i] and x[j] (i<>j; i,j=0,1,...,n) such      }
{              that x[i]=x[j],                                              }
{           0, otherwise.                                                   }
{         Note: If st<>0, then the elements of array a are not calculated.  }
{  Unlocal identifiers:                                                     }
{    vector  - a type identifier of extended array [q0..qn], where q0<=0    }
{              and qn>=n,                                                   }
{    vector1 - a type identifier of extended array [q1..qn2], where q1<=1   }
{              and qn2>=n-2,                                                }
{    vector2 - a type identifier of extended array [q2..qn1], where q2<=2   }
{              and qn1>=n-1,                                                }
{    matrix  - a type identifier of extended array [0..3, q0..qn1], where   }
{              q0<=0 and qn1>=n-1.                                          }
{                                                                           }
{---------------------------------------------------------------------------}
var i,k        : Integer;
    u,v,y,z,xi : Extended;
    d          : vector;
    b          : vector1;
    c          : vector2;
begin
  if n<1
    then st:=1
    else begin
           st:=0;
           i:=-1;
           repeat
             i:=i+1;
             for k:=i+1 to n do
               if x[i]=x[k]
                 then st:=2
           until (i=n-1) or (st=2)
         end;
  if st=0
    then begin
           d[0]:=0;
           d[n]:=0;
           if n>1
             then begin
                    z:=x[2];
                    y:=z-x[1];
                    z:=z-x[0];
                    u:=f[1];
                    b[1]:=y/z;
                    d[1]:=6*((f[2]-u)/y-(u-f[0])/(x[1]-x[0]))/z;
                    z:=x[n-2];
                    y:=x[n-1]-z;
                    z:=x[n]-z;
                    u:=f[n-1];
                    c[n-1]:=y/z;
                    d[n-1]:=6*((f[n]-u)/(x[n]-x[n-1])-(u-f[n-2])/y)/z;
                    for i:=2 to n-2 do
                      begin
                        z:=x[i];
                        y:=x[i+1]-z;
                        z:=z-x[i-1];
                        u:=f[i];
                        b[i]:=y/(y+z);
                        c[i]:=1-b[i];
                        d[i]:=6*((f[i+1]-u)/y-(u-f[i-1])/z)/(y+z)
                      end;
                    u:=2;
                    i:=0;
                    y:=d[1]/u;
                    d[1]:=y;
                    if n>2
                      then repeat
                             i:=i+1;
                             z:=b[i]/u;
                             b[i]:=z;
                             u:=2-z*c[i+1];
                             y:=(d[i+1]-y*c[i+1])/u;
                             d[i+1]:=y
                           until i=n-2
                  end;
           u:=d[n-1];
           for i:=n-2 downto 1 do
             begin
               u:=d[i]-u*b[i];
               d[i]:=u
             end;
           for i:=0 to n-1 do
             begin
               u:=f[i];
               xi:=x[i];
               z:=x[i+1]-xi;
               y:=d[i];
               v:=(f[i+1]-u)/z-(2*y+d[i+1])*z/6;
               z:=(d[i+1]-y)/(6*z);
               y:=y/2;
               a[0,i]:=((-z*xi+y)*xi-v)*xi+u;
               u:=3*z*xi;
               a[1,i]:=(u-2*y)*xi+v;
               a[2,i]:=y-u;
               a[3,i]:=z
             end
         end
end;

procedure TForm1.Button1Click(Sender: TObject);
begin
  if formNumber = 0 then
  begin
    n := strtoint(Edit1.Text);
    Label1.Caption := 'x[0]';
  end
  else if formNumber <= (n+1) then
  begin
    x[formNumber-1] := strToFloat(Edit1.Text);
    if formNumber-1 = n then
      Label1.Caption := 'f[0]'
    else Label1.Caption := 'x[' + intToStr(formNumber) +']'
  end
  else if formNumber <= 2*(n+1) then
  begin
    f[(formNumber-1)mod (n+1)] := strToFloat(Edit1.Text);
    if formNumber = 2*(n+1) then
    begin
      naturalsplinecoeffns(n, x, f, a, st);
      formNumber := 0;
      Label1.Caption := floatToStr(a[0][0]);
      Label2.Caption := floatToStr(a[1][0]);
      Label3.Caption := floatToStr(a[2][0]);
      Label4.Caption := floatToStr(a[3][0]);
      Label5.Caption := intToStr(st);
    end
    else Label1.Caption := 'f[' + intToStr((formNumber)mod (n+1)) +']'
  end;
  formNumber := formNumber + 1;
end;


end.

